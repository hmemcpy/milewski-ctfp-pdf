\lettrine[lhang=0.17]{N}{ow that we have} covered monads, we can reap the benefits of duality and
get comonads for free simply by reversing the arrows and working in the
opposite category.

Recall that, at the most basic level, monads are about composing Kleisli
arrows:

\begin{Verbatim}[commandchars=\\\{\}]
a -> m b
\end{Verbatim}
where \code{m} is a functor that is a monad. If we use the letter
\code{w} (upside down \code{m}) for the comonad, we can define
co-Kleisli arrows as morphism of the type:

\begin{Verbatim}[commandchars=\\\{\}]
w a -> b
\end{Verbatim}
The analog of the fish operator for co-Kleisli arrows is defined as:

\begin{Verbatim}[commandchars=\\\{\}]
(=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
\end{Verbatim}
For co-Kleisli arrows to form a category we also have to have an
identity co-Kleisli arrow, which is called \code{extract}:

\begin{Verbatim}[commandchars=\\\{\}]
extract :: w a -> a
\end{Verbatim}
This is the dual of \code{return}. We also have to impose the laws of
associativity as well as left- and right-identity. Putting it all
together, we could define a comonad in Haskell as:

\begin{Verbatim}[commandchars=\\\{\}]
class Functor w => Comonad w where
    (=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
    extract :: w a -> a
\end{Verbatim}
In practice, we use slightly different primitives, as we'll see shortly.

The question is, what's the use for comonads in programming?

\section{Programming with Comonads}\label{programming-with-comonads}

Let's compare the monad with the comonad. A monad provides a way of
putting a value in a container using \code{return}. It doesn't give
you access to a value or values stored inside. Of course, data
structures that implement monads might provide access to their contents,
but that's considered a bonus. There is no common interface for
extracting values from a monad. And we've seen the example of the
\code{IO} monad that prides itself in never exposing its contents.

A comonad, on the other hand, provides the means of extracting a single
value from it. It does not give the means to insert values. So if you
want to think of a comonad as a container, it always comes pre-filled
with contents, and it lets you peek at it.

Just as a Kleisli arrow takes a value and produces some embellished
result --- it embellishes it with context --- a co-Kleisli arrow takes a
value together with a whole context and produces a result. It's an
embodiment of \newterm{contextual computation}.

\section{The Product Comonad}\label{the-product-comonad}

Remember the reader monad? We introduced it to tackle the problem of
implementing computations that need access to some read-only environment
\code{e}. Such computations can be represented as pure functions of
the form:

\begin{Verbatim}[commandchars=\\\{\}]
(a, e) -> b
\end{Verbatim}
We used currying to turn them into Kleisli arrows:

\begin{Verbatim}[commandchars=\\\{\}]
a -> (e -> b)
\end{Verbatim}
But notice that these functions already have the form of co-Kleisli
arrows. Let's massage their arguments into the more convenient functor
form:

\begin{Verbatim}[commandchars=\\\{\}]
data Product e a = P e a deriving Functor
\end{Verbatim}
We can easily define the composition operator by making the same
environment available to the arrows that we are composing:

\begin{minted}[breaklines, breakafter=->]{text}
(=>=) :: (Product e a -> b) -> (Product e b -> c) -> (Product e a -> c)
f =>= g = \(P e a) -> let b = f (P e a)
                          c = g (P e b) 
                      in c
\end{minted}
The implementation of \code{extract} simply ignores the environment:

\begin{Verbatim}[commandchars=\\\{\}]
extract (P e a) = a
\end{Verbatim}
Not surprisingly, the product comonad can be used to perform exactly the
same computations as the reader monad. In a way, the comonadic
implementation of the environment is more natural --- it follows the
spirit of ``computation in context.'' On the other hand, monads come
with the convenient syntactic sugar of the \code{do} notation.

The connection between the reader monad and the product comonad goes
deeper, having to do with the fact that the reader functor is the right
adjoint of the product functor. In general, though, comonads cover
different notions of computation than monads. We'll see more examples
later.

It's easy to generalize the \code{Product} comonad to arbitrary
product types including tuples and records.

\section{Dissecting the
Composition}\label{dissecting-the-composition}

Continuing the process of dualization, we could go ahead and dualize
monadic bind and join. Alternatively, we can repeat the process we used
with monads, where we studied the anatomy of the fish operator. This
approach seems more enlightening.

The starting point is the realization that the composition operator must
produce a co-Kleisli arrow that takes \code{w a} and produces a
\code{c}. The only way to produce a \code{c} is to apply the second
function to an argument of the type \code{w b}:

\begin{Verbatim}[commandchars=\\\{\}]
(=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
f =>= g = g ... 
\end{Verbatim}
But how can we produce a value of type \code{w b} that could be fed
to \code{g}? We have at our disposal the argument of type
\code{w a} and the function\\ \code{f :: w a -> b}.
The solution is to define the dual of bind, which is called extend:

\begin{Verbatim}[commandchars=\\\{\}]
extend :: (w a -> b) -> w a -> w b
\end{Verbatim}
Using \code{extend} we can implement composition:

\begin{Verbatim}[commandchars=\\\{\}]
f =>= g = g . extend f
\end{Verbatim}
Can we next dissect \code{extend}? You might be tempted to say, why
not just apply the function \code{w a -> b} to the
argument \code{w a}, but then you quickly realize that you'd have no
way of converting the resulting \code{b} to \code{w b}. Remember,
the comonad provides no means of lifting values. At this point, in the
analogous construction for monads, we used \code{fmap}. The only way
we could use \code{fmap} here would be if we had something of the type
\code{w (w a)} at our disposal. If we coud only turn \code{w a}
into\\ \code{w (w a)}. And, conveniently, that would be exactly the
dual of \code{join}. We call it \code{duplicate}:

\begin{Verbatim}[commandchars=\\\{\}]
duplicate :: w a -> w (w a)
\end{Verbatim}
So, just like with the definitions of the monad, we have three
equivalent definitions of the comonad: using co-Kleisli arrows,
\code{extend}, or \code{duplicate}. Here's the Haskell definition
taken directly from\\ \code{Control.Comonad} library:

\begin{Verbatim}[commandchars=\\\{\}]
class Functor w => Comonad w where
    extract :: w a -> a
    duplicate :: w a -> w (w a)
    duplicate = extend id
    extend :: (w a -> b) -> w a -> w b
    extend f = fmap f . duplicate
\end{Verbatim}
Provided are the default implementations of \code{extend} in terms of
\code{duplicate} and vice versa, so you only need to override one of
them.

The intuition behind these functions is based on the idea that, in
general, a comonad can be thought of as a container filled with values
of type \code{a} (the product comonad was a special case of just one
value). There is a notion of the ``current'' value, one that's easily
accessible through \code{extract}. A co-Kleisli arrow performs some
computation that is focused on the current value, but it has access to
all the surrounding values. Think of the Conway's game of life. Each
cell contains a value (usually just \code{True} or \code{False}). A
comonad corresponding to the game of life would be a grid of cells
focused on the ``current'' cell.

So what does \code{duplicate} do? It takes a comonadic container
\code{w a} and produces a container of containers \code{w (w a)}.
The idea is that each of these containers is focused on a different
\code{a} inside \code{w a}. In the game of life, you would get a
grid of grids, each cell of the outer grid containing an inner grid
that's focused on a different cell.

Now look at \code{extend}. It takes a co-Kleisli arrow and a comonadic
container \code{w a} filled with \code{a}s. It applies the
computation to all of these \code{a}s, replacing them with
\code{b}s. The result is a comonadic container filled with
\code{b}s. \code{extend} does it by shifting the focus from one
\code{a} to another and applying the co-Kleisli arrow to each of them
in turn. In the game of life, the co-Kleisli arrow would calculate the
new state of the current cell. To do that, it would look at its context
--- presumably its nearest neighbors. The default implementation of
\code{extend} illustrates this process. First we call
\code{duplicate} to produce all possible foci and then we apply
\code{f} to each of them.

\section{The Stream Comonad}\label{the-stream-comonad}

This process of shifting the focus from one element of the container to
another is best illustrated with the example of an infinite stream. Such
a stream is just like a list, except that it doesn't have the empty
constructor:

\begin{Verbatim}[commandchars=\\\{\}]
data Stream a = Cons a (Stream a)
\end{Verbatim}
It's trivially a \code{Functor}:

\begin{Verbatim}[commandchars=\\\{\}]
instance Functor Stream where
    fmap f (Cons a as) = Cons (f a) (fmap f as)
\end{Verbatim}
The focus of a stream is its first element, so here's the implementation
of \code{extract}:

\begin{Verbatim}[commandchars=\\\{\}]
extract (Cons a _) = a
\end{Verbatim}
\code{duplicate} produces a stream of streams, each focused on a
different element.

\begin{Verbatim}[commandchars=\\\{\}]
duplicate (Cons a as) = Cons (Cons a as) (duplicate as)
\end{Verbatim}
The first element is the original stream, the second element is the tail
of the original stream, the third element is its tail, and so on, ad
infinitum.

Here's the complete instance:

\begin{Verbatim}[commandchars=\\\{\}]
instance Comonad Stream where
    extract (Cons a _) = a 
    duplicate (Cons a as) = Cons (Cons a as) (duplicate as)
\end{Verbatim}
This is a very functional way of looking at streams. In an imperative
language, we would probably start with a method \code{advance} that
shifts the stream by one position. Here, \code{duplicate} produces all
shifted streams in one fell swoop. Haskell's laziness makes this
possible and even desirable. Of course, to make a \code{Stream}
practical, we would also implement the analog of \code{advance}:

\begin{Verbatim}[commandchars=\\\{\}]
tail :: Stream a -> Stream a
tail (Cons a as) = as
\end{Verbatim}
but it's never part of the comonadic interface.

If you had any experience with digital signal processing, you'll see
immediately that a co-Kleisli arrow for a stream is just a digital
filter, and \code{extend} produces a filtered stream.

As a simple example, let's implement the moving average filter. Here's a
function that sums \code{n} elements of a stream:

\begin{Verbatim}[commandchars=\\\{\}]
sumS :: Num a => Int -> Stream a -> a
sumS n (Cons a as) = if n <= 0 then 0 else a + sumS (n - 1) as
\end{Verbatim}
Here's the function that calculates the average of the first \code{n}
elements of the stream:

\begin{Verbatim}[commandchars=\\\{\}]
average :: Fractional a => Int -> Stream a -> a
average n stm = (sumS n stm) / (fromIntegral n)
\end{Verbatim}
Partially applied \code{average n} is a co-Kleisli arrow, so we can
\code{extend} it over the whole stream:

\begin{Verbatim}[commandchars=\\\{\}]
movingAvg :: Fractional a => Int -> Stream a -> Stream a
movingAvg n = extend (average n)
\end{Verbatim}
The result is the stream of running averages.

A stream is an example of a unidirectional, one-dimensional comonad. It
can be easily made bidirectional or extended to two or more dimensions.

\section{Comonad Categorically}\label{comonad-categorically}

Defining a comonad in category theory is a straightforward exercise in
duality. As with the monad, we start with an endofunctor \code{T}. The
two natural transformations, η and μ, that define the monad are simply
reversed for the comonad:

\begin{Verbatim}[commandchars=\\\{\}]
ε :: T -> I
δ :: T -> T\textsuperscript{2}
\end{Verbatim}
The components of these transformations correspond to \code{extract}
and \code{duplicate}. Comonad laws are the mirror image of monad laws.
No big surprise here.

Then there is the derivation of the monad from an adjunction. Duality
reverses an adjunction: the left adjoint becomes the right adjoint and
vice versa. And, since the composition \code{R ◦ L} defines a monad,
\code{L ◦ R} must define a comonad. The counit of the adjunction:

\begin{Verbatim}[commandchars=\\\{\}]
ε :: L ◦ R -> I
\end{Verbatim}
is indeed the same ε that we see in the definition of the comonad ---
or, in components, as Haskell's \code{extract}. We can also use the
unit of the adjunction:

\begin{Verbatim}[commandchars=\\\{\}]
η :: I -> R ◦ L
\end{Verbatim}
to insert an \code{R ◦ L} in the middle of \code{L ◦ R} and
produce \code{L ◦ R ◦ L ◦ R}. Making \code{T\textsuperscript{2}} from \code{T}
defines the δ, and that completes the definition of the comonad.

We've also seen that the monad is a monoid. The dual of this statement
would require the use of a comonoid, so what's a comonoid? The original
definition of a monoid as a single-object category doesn't dualize to
anything interesting. When you reverse the direction of all
endomorphisms, you get another monoid. Recall, however, that in our
approach to a monad, we used a more general definition of a monoid as an
object in a monoidal category. The construction was based on two
morphisms:

\begin{Verbatim}[commandchars=\\\{\}]
μ :: m \ensuremath{\otimes} m -> m
η :: i -> m
\end{Verbatim}
The reversal of these morphisms produces a comonoid in a monoidal
category:

\begin{Verbatim}[commandchars=\\\{\}]
δ :: m -> m \ensuremath{\otimes} m
ε :: m -> i
\end{Verbatim}
One can write a definition of a comonoid in Haskell:

\begin{Verbatim}[commandchars=\\\{\}]
class Comonoid m where
    split :: m -> (m, m)
    destroy :: m -> ()
\end{Verbatim}
but it is rather trivial. Obviously \code{destroy} ignores its
argument.

\begin{Verbatim}[commandchars=\\\{\}]
destroy _ = ()
\end{Verbatim}
\code{split} is just a pair of functions:

\begin{Verbatim}[commandchars=\\\{\}]
split x = (f x, g x)
\end{Verbatim}
Now consider comonoid laws that are dual to the monoid unit laws.

\begin{Verbatim}[commandchars=\\\{\}]
lambda . bimap destroy id . split = id
rho . bimap id destroy . split = id
\end{Verbatim}
Here, \code{lambda} and \code{rho} are the left and right unitors,
respectively (see the definition of
\hyperref[monads-categorically]{monoidal
categories}). Plugging in the definitions, we get:

\begin{Verbatim}[commandchars=\\\{\}]
lambda (bimap destroy id (split x))
= lambda (bimap destroy id (f x, g x))
= lambda ((), g x)
= g x
\end{Verbatim}
which proves that \code{g = id}. Similarly, the second law expands
to \code{f\ =\ id}. In conclusion:

\begin{Verbatim}[commandchars=\\\{\}]
split x = (x, x)
\end{Verbatim}
which shows that in Haskell (and, in general, in the category
$\Set$) every object is a trivial comonoid.

Fortunately there are other more interesting monoidal categories in
which to define comonoids. One of them is the category of endofunctors.
And it turns out that, just like the monad is a monoid in the category
of endofunctors,

\begin{quote}
The comonad is a comonoid in the category of endofunctors.
\end{quote}

\section{The Store Comonad}\label{the-store-comonad}

Another important example of a comonad is the dual of the state monad.
It's called the costate comonad or, alternatively, the store comonad.

We've seen before that the state monad is generated by the adjunction
that defines the exponentials:

\begin{Verbatim}[commandchars=\\\{\}]
L z = z × s
R a = s \ensuremath{\Rightarrow} a
\end{Verbatim}
We'll use the same adjunction to define the costate comonad. A comonad
is defined by the composition \code{L ◦ R}:

\begin{Verbatim}[commandchars=\\\{\}]
L (R a) = (s ⇒ a) × s
\end{Verbatim}
Translating this to Haskell, we start with the adjunction between the
\code{Prod} functor on the left and the \code{Reader} functor or the
right. Composing \code{Prod} after \code{Reader} is equivalent to
the following definition:

\begin{Verbatim}[commandchars=\\\{\}]
data Store s a = Store (s -> a) s
\end{Verbatim}
The counit of the adjunction taken at the object \code{a} is the
morphism:

\begin{Verbatim}[commandchars=\\\{\}]
ε\textsubscript{a} :: ((s \ensuremath{\Rightarrow} a) × s) -> a
\end{Verbatim}
or, in Haskell notation:

\begin{Verbatim}[commandchars=\\\{\}]
counit (Prod (Reader f, s)) = f s
\end{Verbatim}
This becomes our \code{extract}:

\begin{Verbatim}[commandchars=\\\{\}]
extract (Store f s) = f s
\end{Verbatim}
The unit of the adjunction:

\begin{Verbatim}
unit a = Reader (\s -> Prod (a, s))
\end{Verbatim}
can be rewritten as partially applied data constructor:

\begin{Verbatim}[commandchars=\\\{\}]
Store f :: s -> Store f s
\end{Verbatim}
We construct δ, or \code{duplicate}, as the horizontal composition:

\begin{Verbatim}[commandchars=\\\{\}]
δ :: L ◦ R -> L ◦ R ◦ L ◦ R
δ = L ◦ η ◦ R
\end{Verbatim}
We have to sneak η through the leftmost \code{L}, which is the
\code{Prod} functor. It means acting with η, or \code{Store f}, on
the left component of the pair (that's what \code{fmap} for
\code{Prod} would do). We get:

\begin{Verbatim}[commandchars=\\\{\}]
duplicate (Store f s) = Store (Store f) s
\end{Verbatim}
(Remember that, in the formula for δ, \code{L} and \code{R} stand
for identity natural transformations whose components are identity
morphisms.)

Here's the complete definition of the \code{Store} comonad:

\begin{Verbatim}[commandchars=\\\{\}]
instance Comonad (Store s) where
    extract (Store f s) = f s
    duplicate (Store f s) = Store (Store f) s
\end{Verbatim}
You may think of the \code{Reader} part of \code{Store} as a
generalized container of \code{a}s that are keyed using elements of
the type \code{s}. For instance, if \code{s} is \code{Int},
\code{Reader Int a} is an infinite bidirectional stream of
\code{a}s. \code{Store} pairs this container with a value of the key
type. For instance, \code{Reader Int a} is paired with an
\code{Int}. In this case, \code{extract} uses this integer to index
into the infinite stream. You may think of the second component of
\code{Store} as the current position.

Continuing with this example, \code{duplicate} creates a new infinite
stream indexed by an \code{Int}. This stream contains streams as its
elements. In particular, at the current position, it contains the
original stream. But if you use some other \code{Int} (positive or
negative) as the key, you'd obtain a shifted stream positioned at that
new index.

In general, you can convince yourself that when \code{extract} acts on
the \code{duplicate}d \code{Store} it produces the original
\code{Store} (in fact, the identity law for the comonad states that
\code{extract . duplicate = id}).

The \code{Store} comonad plays an important role as the theoretical
basis for the \code{Lens} library. Conceptually, the
\code{Store s a} comonad encapsulates the idea of ``focusing'' (like
a lens) on a particular substructure of the date type \code{a} using
the type \code{s} as an index. In particular, a function of the type:

\begin{Verbatim}[commandchars=\\\{\}]
a -> Store s a
\end{Verbatim}
is equivalent to a pair of functions:

\begin{Verbatim}[commandchars=\\\{\}]
set :: a -> s -> a
get :: a -> s
\end{Verbatim}
If \code{a} is a product type, $\Set$ could be implemented as
setting the field of type \code{s} inside of \code{a} while
returning the modified version of \code{a}. Similarly, \code{get}
could be implemented to read the value of the \code{s} field from
\code{a}. We'll explore these ideas more in the next section.

\section{Challenges}\label{challenges}

\begin{enumerate}
\tightlist
\item
  Implement the Conway's Game of Life using the \code{Store} comonad.
  Hint: What type do you pick for \code{s}?
\end{enumerate}